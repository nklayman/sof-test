// SPDX-License-Identifier: BSD-3-Clause
// Copyright(c) 2022 Intel Corporation. All rights reserved.

// This tracks the time it takes for ipc communications (sof_ipc_tx_message execution time)
// It prints out timings as they happen, and will generate a histogram
// when you stop the script with ctrl-c

kprobe:sof_ipc_tx_message {
  // Save time right away as it changes during bpftrace script execution
  @nsecs = nsecs;
  if (@start != 0 ) {
    printf("ERROR: overlapping tx");
    exit();
  }
  @start = @nsecs;
  // We could get also the name of the device and other properties from the args,
  // but we'd need to enable DEBUG_INFO and DEBUG_INFO_BTF to get the types
  // It would allow us to sort timings by device for example
}

kretprobe:sof_ipc_tx_message {
  @duration = nsecs - @start;
  @usecs = hist(@duration / 1000);
  @avg = avg(@duration / 1000);
  @start = 0;
}

END {
  // Clean up variables that shouldn't be printed
  delete(@start);
  delete(@duration);
}
